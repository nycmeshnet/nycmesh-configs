# NYC Mesh Mikrotik SXTsq 5ac config
# kiosk vpn ospf
:global nodenumber {{nodenumber}}

:global cidr ("10." . ((96+(nodenumber>>10))+0) . "." . (((nodenumber>>2)&255)+0) . "." . (((nodenumber&3)<<6)+0) . "/26")
:global ipthirdoctet ( [ :pick $nodenumber ([:len $nodenumber] - 5) ([:len $nodenumber] - 2) ] + 0 )
:global ipfourthoctet ( [ :pick $nodenumber ([:len $nodenumber] - 2) ([:len $nodenumber]) ] + 0 )

:global cidrleft [ :pick $cidr 0 ( [ :find $cidr "/" ] ) ]
:global cidrright [ :pick $cidr (( [ :find $cidr "/" ] )+1) 100 ]
:global netmask (255.255.255.255<<(32-$cidrright))
:global subnet ($cidrleft&$netmask)
:global firstip ($subnet+1)
:global lastip ($subnet + (~($subnet|$netmask)) - 1)
:global dhcprange (($firstip+5) . "-" . ($lastip-5))
:global meship ("10.69." . $ipthirdoctet . "." . ($ipfourthoctet + 100))
:global wdsip ("10.68." . $ipthirdoctet . "." . ($ipfourthoctet + 100))

/delay 15

/ip address add address=192.168.88.1/24 comment=defconf interface=ether1 network=192.168.88.0

:foreach x in=[/interface wireless find] do={ /interface wireless reset-configuration $x }

/interface bridge
add auto-mac=yes name=mesh fast-forward=no protocol-mode=none
add auto-mac=yes name=wds fast-forward=no protocol-mode=none

/interface bridge settings
set use-ip-firewall=yes


/interface wireless security-profiles
add authentication-types=wpa-psk,wpa2-psk management-protection=allowed mode=\
    dynamic-keys name=nycmeshnet supplicant-identity=nycmesh \
    wpa-pre-shared-key=nycmeshnet wpa2-pre-shared-key=nycmeshnet

/interface wireless security-profiles
add authentication-types=wpa-eap,wpa2-eap eap-methods=eap-ttls-mschapv2 group-ciphers=tkip,aes-ccm mode=dynamic-keys mschapv2-password=5fsOpxER mschapv2-username=anonymous@citybridge.com name=linknyc supplicant-identity=anonymous@citybridge.com tls-mode=dont-verify-certificate unicast-ciphers=tkip,aes-ccm


/interface wireless
set [ find default-name=wlan1 ] band=5ghz-a/n/ac channel-width=20/40/80mhz-Ceee country="united states2" default-authentication=no disabled=no frequency=auto security-profile=linknyc ssid="LinkNYC Private" wireless-protocol=802.11

/interface wireless connect-list
add interface=wlan1 security-profile=linknyc ssid="LinkNYC Private" wireless-protocol=802.11

/interface l2tp-client add allow-fast-path=yes connect-to=l2tpvpn.sn1.mesh.nycmesh.net disabled=no ipsec-secret=nycmeshnet max-mru=1200 max-mtu=1200 keepalive-timeout=disabled name=nycmesh-375p-core2 password=nycmeshnet use-ipsec=yes user=nycmesh

/ip address
add address=($firstip . "/" . $cidrright) interface=mesh
add address=($meship . "/16") interface=mesh
add address=($wdsip . "/16") interface=wds

/ip dhcp-client
add add-default-route=yes default-route-distance=210 disabled=no interface=wlan1 use-peer-dns=no use-peer-ntp=no script=":local currentGateway [ /ip dhcp-client get [/ip dhcp-client find status=bound ] gateway ]\
    \n/ip route remove [ /ip route find dst-address=199.167.59.6/32 ]\
    \n/ip route remove [ /ip route find dst-address=8.8.8.8/32 ]\
    \n/ip route add dst-address=199.167.59.6/32 gateway=\$currentGateway\
    \n/ip route add dst-address=199.170.132.6/32 gateway=\$currentGateway\
    \n/ip route add dst-address=8.8.8.8/32 gateway=\$currentGateway"

/ip dns set allow-remote-requests=yes
/ip dns set allow-remote-requests=yes servers=10.10.10.10,8.8.8.8,8.8.4.4

/interface bridge port
add bridge=mesh interface=ether1

/interface bridge filter
add action=drop chain=forward in-bridge=mesh

/ip pool
add name=local ranges=$dhcprange

/ip dhcp-server
add address-pool=local disabled=no interface=mesh name=localdhcp

/ip dhcp-server network
add address=$cidr dns-server=("10.10.10.10," . $firstip) gateway=$firstip netmask=$cidrright

/routing filter add chain=ospf-in set-bgp-communities=65000:110 set-distance=205
/routing filter add action=jump chain=ospf-out jump-target=meshaddrs
/routing filter add action=discard chain=meshaddrs prefix=10.70.72.1 prefix-length=32
/routing filter add action=accept chain=meshaddrs prefix=10.0.0.0/8 prefix-length=18-32
/routing filter add action=accept chain=meshaddrs prefix=0.0.0.0/0
/routing filter add action=discard chain=meshaddrs

/routing ospf instance set [ find default=yes ] router-id=$meship redistribute-connected=as-type-1 metric-default=500
/routing filter add chain="ospf-in" set-bgp-communities=65000:110 set-distance=205
/routing ospf interface add interface=mesh network-type=ptmp
/routing ospf interface add interface=wds network-type=ptmp
/routing ospf interface add cost=50 interface=nycmesh-375p-core2 network-type=ptmp
/routing ospf network add area=backbone network=10.69.0.0/16
/routing ospf network add area=backbone network=10.68.0.0/16
/routing ospf network add area=backbone network=10.70.72.0/24

/ip firewall address-list
add address=10.0.0.0/8 list=meshaddr
add address=199.167.59.0/24 list=meshaddr
add address=199.170.132.0/24 list=meshaddr

/ip firewall filter
add action=accept chain=input connection-state=established,related,untracked
add action=drop chain=input connection-state=invalid
add action=accept chain=input protocol=icmp
add action=drop chain=input in-interface=wlan1
add action=drop chain=input src-address-list=!meshaddr
add action=fasttrack-connection chain=forward connection-state=established,related
add action=accept chain=forward connection-state=established,related,untracked
add action=accept chain=forward ipsec-policy=in,ipsec
add action=accept chain=forward ipsec-policy=out,ipsec
add action=drop chain=forward connection-state=invalid
add action=drop chain=forward connection-nat-state=!dstnat connection-state=new in-interface=wlan1

/ip firewall nat add action=masquerade chain=srcnat ipsec-policy=out,none out-interface=wlan1

/ip firewall service-port
set ftp disabled=yes
set tftp disabled=yes
set irc disabled=yes
set h323 disabled=yes
set sip disabled=yes
set pptp disabled=yes
set udplite disabled=yes
set dccp disabled=yes
set sctp disabled=yes

/snmp set enabled=yes

/system identity set name=("nycmesh-" . $nodenumber . "-sxtsq")

/system clock set time-zone-name=America/New_York
/system ntp client
set enabled=yes primary-ntp=10.10.10.123 server-dns-names=0.pool.ntp.org


/tool netwatch add down-script="/ip dhcp-client release 0" host=8.8.8.8 interval=2m timeout=2s

/ip address remove [ /ip address find network=192.168.88.0 ]

